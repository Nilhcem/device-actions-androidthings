apply plugin: 'com.google.protobuf'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.sdk_version
    buildToolsVersion rootProject.ext.build_tools_version

    defaultConfig {
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.sdk_version

        buildConfigField 'String', 'ASSISTANT_LANGUAGE_CODE', '"en-US"'
        buildConfigField 'String', 'ASSISTANT_DEVICE_INSTANCE_ID', '"my-device-id"'
        buildConfigField 'String', 'ASSISTANT_DEVICE_MODEL_ID', '"my-model"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext.grpc_version = '1.9.0'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }

    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation "android.arch.lifecycle:extensions:$arch_version"
    kapt "android.arch.lifecycle:compiler:$arch_version"

    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "io.grpc:grpc-protobuf-lite:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "io.grpc:grpc-auth:$grpc_version"
    implementation('com.google.auth:google-auth-library-oauth2-http:0.9.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'com.squareup.moshi:moshi:1.5.0'
}
